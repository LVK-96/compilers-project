Program -> Declaration-list #END
Declaration-list -> Declaration Declaration-list | EPSILON
Declaration -> Declaration-initial Declaration-prime
Declaration-initial -> Type-specifier #PID ID
Declaration-prime -> #FUNCTION Fun-declaration-prime | #VARIABLE Var-declaration-prime
Var-declaration-prime -> ; | [ NUM ] ;
Fun-declaration-prime -> #BEGINSCOPE ( #START_PARAM_COUNTER Params #STOP_PARAM_COUNTER ) Compound-stmt #ENDSCOPE
Type-specifier -> int | void
Params -> int ID #PARAM Param-prime Param-list | void #PARAM Param-list-void-abtar
Param-list-void-abtar -> ID Param-prime Param-list | EPSILON
Param-list -> , Param Param-list | EPSILON
Param -> Declaration-initial #PARAM Param-prime
Param-prime -> [ ] | EPSILON
Compound-stmt -> { Declaration-list Statement-list }
Statement-list -> Statement Statement-list | EPSILON
Statement -> Expression-stmt | #BEGINSCOPE Compound-stmt #ENDSCOPE | Selection-stmt | Iteration-stmt | Return-stmt | Switch-stmt
Expression-stmt -> Expression ; | continue ; | break ; | ;
Selection-stmt -> if ( Expression ) Statement else Statement
Iteration-stmt -> while ( Expression ) Statement
Return-stmt -> return Return-stmt-prime
Return-stmt-prime -> ; | Expression ;
Switch-stmt -> switch ( Expression ) { Case-stmts Default-stmt }
Case-stmts -> Case-stmt Case-stmts | EPSILON
Case-stmt -> case NUM : Statement-list
Default-stmt -> default : Statement-list | EPSILON
Expression -> Simple-expression-zegond | #PID ID B
B -> = Expression | [ Expression ] H | Simple-expression-prime
H -> = Expression | G D C
Simple-expression-zegond -> Additive-expression-zegond C
Simple-expression-prime -> Additive-expression-prime C
C -> Relop Additive-expression | EPSILON
Relop -> < | ==
Additive-expression -> Term D
Additive-expression-prime -> Term-prime D
Additive-expression-zegond -> Term-zegond D
D -> Addop Term D | EPSILON
Addop -> + | -
Term -> Factor G
Term-prime -> Factor-prime G
Term-zegond -> Factor-zegond G
G -> * Factor G | EPSILON
Factor -> ( Expression ) | #PID ID Var-call-prime | NUM
Var-call-prime -> ( Args ) | Var-prime
Var-prime -> [ Expression ] | EPSILON
Factor-prime -> ( Args ) | EPSILON
Factor-zegond -> ( Expression ) | NUM
Args -> Arg-list | EPSILON
Arg-list -> Expression Arg-list-prime
Arg-list-prime -> , Expression Arg-list-prime | EPSILON
